%Week 11: Times Series Part 2
%Samuel Rowe Adapted from Wooldridge

	clear
	set more off

<h3>Topics</h3>
<ul>
<li>Unit Roots</li>
<li>Testing for Serial Correlation</li>
<li>Correcting for Serial Correlation</li>
<li>Forecasting</li>
</ul>

<h2>Wooldridge</h2>
Set Working Directory
	cd "/Users/Sam/Desktop/Econ 645/Data/Wooldridge"

<h3>Unit Roots</h3>
<h5>Lesson: when we have a unit root process or Time Series integrated of order one I(1)
we can use a first difference to transform the time series into a weakly dependent
and often stationary process.</h5>

<h4>Fertility</h4> 
	use fertil3.dta, clear

Set Time Series
	tsset year

	twoway line gfr year, title("General Fertility Rate") ytitle("Children Born per 1,000 CBA Women") xtitle("Year")
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_i1.png", replace
![General Fertility Rate](week_11_i1.png)

Estimate autocorrelation or estimate rho-hat
	reg gfr l.gfr

Our result is rho-hat is 0.98, which is indicative of a unit root process.
Under our TS assumptions our t statistics are invalid, but if we use a 
transformation using a first-difference process, we can relax those assumptions
under the TSC assumptions. If our I(1) becomes I(0), then our TSC assumptions
are potentially valid.

First Difference both dependent and independent
	reg d.gfr d.pe if year < 1985

Our result is that the real value of the personal exemption is associated with
a 0.043 decrease in fertility or a 23 dollar increase in the real value of the
personal exemption is associated with a 1 child per capita (1,000 CBA Women).
This is a bit unexpected, but still insignificant. Let's add some lags.

First differnce with more than 1 lag
	reg d.gfr d.pe d.pe_1 d.pe_2 if year < 1985

Our results show that increases in the current and prior period real value 
personal exemption is not statistically significant. However, a 1 dollar increase
in the real value of personal exemption is associated two period prior is associated
with an increase of .110 child per capita (or $9.1 increase is associated with
an increase of 1 child per capita.


<h4>Wages and Productivity</h4>

I(1) process with the presence of a linear trend
	use earns.dta, clear

Set Time Series
	tsset year, yearly

We want to estimate the elasticity of hourly wage with respect to output per hour
(or labor productivity). 
$$ ln(hrwage_t)=\beta_0 + \beta_1 ln(outphr_t) + \beta_2 t + u_t $$

We estimate the contemporaneous model 
	reg lhrwage loutphr t

We estimate that the elasticity is very large, where a 1% increase in labor
productivity (output per hour) is associated with a 1.64% increase in hourly
wage. Let's test for autocorrelation with accounting for the linear trend.
	reg lhrwage l.lhrwage t

We have some evidence for a unit root process, so we'll use the first-difference
to transform the I(1) process into a I(0) process. We'll no longer need the 
time trend
	twoway line lhrwage year, title("Natural Log of Hourly Wages") ytitle("Ln(Hourly Wage)") xtitle("Year")
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_hrwage1.png", replace
![Natural Log of Hourly Wages](week_11_hrwage1.png)
	twoway line d.lhrwage year, title("First-Difference in Ln(Hourly Wages)") ytitle("Delta Ln(Hourly Wage)") xtitle("Year")
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_hrwage2.png", replace
![Difference in LN of Hourly Wage](week_11_hrwage2.png)

We'll keep the trend based upon our prior graph
	reg d.lhrwage d.loutphr t

Our transformed model shows that a 1% increase in output per hour is associated
with a .55% increase in hourly wage.


<h3>Dynamically Complete</h3>

<h5>Lesson: Is our model dynamically complete? Use theory/literature, but test, test, test.</h5>
	use fertil3.dta

Set Time Series
	tsset year, yearly

We'll estimate a Finite Distributed Lag model for the first difference of 
general fertility onto the first difference of personal exemptions.
If our model is dynamically complete, then no additional lags of gfr or pe
are needed.

Our First-difference model with one lag of pe
	reg d.gfr d.pe d.pe_1 if year < 1985
Is this dynamically complete? Let's add a second lag of pe
	reg d.gfr d.pe d.pe_1 d.pe_2 if year < 1985
Add lag of d.gfr and see it wasn't dynamically complete
	reg d.gfr d.gfr_1 d.pe d.pe_1 d.pe_2 if year < 1985

<h3>Testing for Serial Correlation</h3>

<h4>Phillip's Curve</h4>
<h5>Lesson: using our t-test, Durban Watson test, and Durban's alternative statistic to test for serial correlation</h5>
We'll use our t-test for serial correlation with strictly exogenous regressors
under the assumption that our regressors are exogenous.
	use phillips.dta, clear

Set Time Series
	tsset year, yearly

We estimated a static Phillips Curve and an augmented Phillips Curve last week.
We'll retest the models for serial correlation.

Test for serial correlation in Static Model
Noticeable serial correlation
	reg inf unem if year < 1997
	predict u, resid

	reg u l.u, noconst

Test for serial correlation in Augmented
Serial correlation is removed with the first-difference in inflation
	reg d.inf unem if year < 1997
	predict u2, resid

	reg u2 l.u2, noconst

Test change in error terms for FD assumption on serial correlation so that
the differences in errors are not correlated.
	gen u2_1=l.u2

	reg d.u2 d.u2_1, noconst
drop u u2

We fail to reject the null hypothesis that there is no serial correlation in
the difference in error term
We can also use the estat dwatson or dwstat
	reg inf l.inf unem if year < 1997
	estat dwatson
Or
	dwstat
DW(3,48)=1.48 and dL ~ 1.4 and dU ~ 1.67, so our DW test is inconclusive
https://www3.nd.edu/~wevans1/econ30331/Durbin_Watson_tables.pdf
We'll look at Durban-Watson and DW Tables again a bit later

<h4>Puerto Rican Employment and Minimum Wage</h4>
<h5>Lesson: We can use Durban's alternative statistic to test for serial correlation
if our regressors are not strictly exogenous.</h5>
Durban's alternative statistic
	use prminwge, clear

Set Time Series
	tsset year

We'll rerun our static model and regress the natural log of the employment-population
ratio onto the natural log of the importance of minimum wage, the natural log
of Puerto Rican Gross National Product, the natural log of US GNP, and a time trend.
	reg lprepop lmincov lprgnp lusgnp t if year < 1988
	predict u

Test Serial Correlation with Durbin's alternative statistic, which is valid when 
we don't have strictly exogenous regressors.
	reg u l.u lmincov lprgnp lusgnp t, noconst 

We can see that we reject the null hypothesis, since our rho-hat is 0.54 and
statistically significant.

<h4>Test AR(3) serial correlation</h4>
<h5>Lesson: We can test for serial correlation among prior periods beside AR(1).</h5>
	use barium.dta, clear

Set Time Series
	tsset t, monthly

We'll look at our barium chloride model again to see if ITC complaints affect
behavior of foreign exporters. We may have higher order of serial correlation
since we are using monthly data.

	reg lchnimp lchempi lgas lrtwex befile6 affile6 afdec6
	predict u

Test for p1=p2=p3=0 jointly with an F-test.
	reg u l.u l2.u l3.u lchempi lgas lrtwex befile6 affile6 afdec6, noconst

Our reject the null hypothesis that all of the lags are equal to zero using an F-test.
	test l.u l2.u l3.u
However, when we test our last two lags, they are jointly insignificant.
	test l2.u l3.u
	drop u

We have evidence of at least serial correlation to the order of one.
<h3>Correct for Serial Correlation</h3>
<h4>Prais-Winsten Estimation in the Event Study</h4>
<h5>Lesson: We'll account for serial correlation with a FGLS Prais-Winsten model.</h5>
	use barium.dta, clear

Set Time Series
	tsset t, monthly
	est clear
OLS
	eststo OLS: reg lchnimp lchempi lgas lrtwex befile6 affile6 afdec6
Prais-Winsten
	eststo Prais: prais lchnimp lchempi lgas lrtwex befile6 affile6 afdec6
	predict u


Our beta-hats in Prais-Winsten are similar to our OLS esimates, but our PW
standard errors account for the serial correlation. Our OLS standard errors
usually understate the actual sampling variation in the OLS estimators and 
should be treated with suspicion.
	esttab, mtitle stat(N rho)

There is serial correlation, but we account for it with the PW FGLS estimator
	reg u l.u lchnimp lchempi lgas lrtwex befile6 affile6 afdec6, noconst
	drop u

<h4>Inflation and Prais</h4>
<h5>Lesson: our PW estimator might not be unbiased or consistent if our assumptions fail.</h5>

We'll show another example comparing OLS and Prais-Winsten estimators. We'll
look at the static Phillips curve, which we know has serial correlation.
	use phillips.dta, clear

Set the Time Series
	tsset year


	est clear
OLS
	eststo OLS: reg inf unem if year < 1997
Prais-Winsten 
	eststo PW: prais inf unem if year < 1997
Prais-Winsten with Cochrane-Orcutt Transformation
	eststo PW_CO: prais inf unem if year < 1997, corc
FD
	eststo FD: reg d.inf d.unem if year < 1997

	esttab, mtitle stat(N rho)

The OLS and PW estimators might give very different estimators if our 
assumptions of strict exogeneity and the a
 $$ Cov(x_(t+1)+x_(t-1),u_t)=0 $$
fail.  If that is the case then using OLS with a first-difference might be
a better method.
We can see that the difference is quite notable. With our OLS estimators, the
tradeoff between inflation and employment is non-existent, but with the PW
estimator our estimate of beta 1 is closer to our first-difference estimate
of beta 1.

It is possible that there is no relationship with the static model, but
the change in inflation and the change in unemployment are negatively related.

<h4>Differencing and Serial Correlation</h4>
<h4>Inflation and Deficits on Interest Rates</h4>
<h5>Lesson: First-differencing is a potential method to eliminate serial correlation.</h5>
	use intdef.dta, clear

Set the Time Series
	tsset year

We'll look at the relationship between interest rates from the 3-month T-bill rate (i3) 
and inflation rate (inf) and deficits as a percentage of GDP. 
Static Model
	reg i3 inf def if year < 2004
Get Durban-Watson Statistic
	dwstat
Critical values d(3,56)=.716, at the 5% level dL = 1.452 dU=1.681
dwstat < dL so we reject the null hypothesis

	predict u
	reg u l.u, noconst
	drop u

We find that we have notable serial correlation in the error term that is statistically
significant. We'll run a first-difference model next.

First differencing
	reg d.i3 d.inf d.def if year < 2004
	dwstat
Critical values d(3,56)=1.796, at the 5% level dL = 1.452 dU=1.681
dwstat > dU so we fail to reject the null hypothesis
	predict u
	reg u l.u, noconst

The serial correlation is no longer a problem with our first difference model, 
since first differencing can transform a unit root process I(1) into a I(0) process.


<h4>Serial Correlation Robust Standard Errors<h4>
<h5>Lesson: We can use the Newey estimator for estimates</h5>
with Heteroskedastic and Autocorrelation Consistent (HAC) standard errors.
	use prminwge.dta, clear

We'll revisit our Puerto Rican examination of the impact of minimum wage importance
We'll use Heteroskedastic and Autocorrelation Consistent (HAC) standard errors, and 
we'll compare OLS, OLS with HAC standard errors, and Prais-Winsten estimates
We'll allow a maximum lag order of autocorrelation up to 2 with the lag(2) option.

Set Time Series
	tsset year, yearly

	est clear
OLS
	eststo OLS: reg lprepop lmincov lprgnp lusgnp t if year < 1988
Newey with lag order 1
	eststo Newey1: newey lprepop lmincov lprgnp lusgnp t if year < 1988, lag(1)
Newey with lag order 2
	eststo Newey2: newey lprepop lmincov lprgnp lusgnp t if year < 1988, lag(2)
Prais-Winsten 
	eststo PW: prais lprepop lmincov lprgnp lusgnp t if year < 1988

With t-stats below estimated parameters
	esttab, mtitle scalars(F N)
With standard errors below the estimated parameters
	esttab, mtitle se scalars(F N)

Notice how our t-stat have fallen and our standard errors have risen for our
Newey1 and Newey2 compared to OLS. Also, notice how the Prais-Winsten beta-hat
on minimum coverage is closer to 0, then OLS or OLS with HAC standard errors.

<h4>Heteroskedasticity</h4>

	use nyse.dta, clear

We'll revisit our test of the Efficient Market Hypothesis with stock return data.
We can test to see if our assumption of homoskedasticity is valid with our
time series. This is a separate test than the test for serial correlation.

Set TIME Series
	tsset t, weekly
OLS
	reg return l.return
We'll test heteroskedasticity with estat hettest
	estat hettest
We'll manually calculate the Breusch-Pagan test
	predict u, residu
	gen u2=u^2
	reg u2 l.return

Test for serial correlation
	reg u l.u, noconst

We can see we reject the null hypothesis that the variance of the error term
is constant, and heteroskedasticity is a problem.

We can use robust, or HAC standard errors, but since serial correlation is 
not a problem, then we don't need newey estimator.
	reg return l.return, robust
	newey return l.return, lag(1)

Expected value does not depend on past returns, but the variance of the error
term is not constant, and needs to be corrected.

<h3>Forecasting</h3>

	use phillips.dta, clear

We'll use our Phillip's Curve data to forecast unemployment one-step into the future

Set Time Series
	tsset year

tsline
	tsline inf
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_inflation.png", replace
![Graph of Inflation Rate](week_11_inflation.png)

First we need to Generate training and test samples, so we'll use the time 
series sequence until 1996 as our training data, and we'll use 1997 to 2003
as our testing data.
	gen test = 0
	replace test = 1 if year >= 1997
	label define test1 0 "training sample" 1 "testing sample"
	label values test test1

We'll use two models to forecast unemployment:
<ul>
<li>Just a 1-year lag of unemployment</li>
<li>1-year lag of unemployment and 1-year lag of inflation</li>
</ul>
Compare models
OLS AR(1)
	reg unem l.unem if test == 0
VAR - Add Lagged Inflation
	reg unem l.unem l.inf if test == 0

<h5>Forecasting</h5>
One-step ahead we can use predict or forecast for AR(1)
	quietly reg unem l.unem if test == 0
	estimates store model1
We can get one-step ahead just using predict
	predict unem_est
Forecast - One-step-ahead forecast
	forecast create forecast1, replace
	forecast estimates model1
Static is our key option otherwise it will be dynamic forecast
	forecast solve, prefix(st_) begin(1997) end(2003) static
Dynamic model - We won't use l.unem but l.dy_unem
	forecast solve, prefix(dy_) begin(1997) end(2003)
Graph Actual, One-Step-Ahead Model, and Dynamic Model
	twoway line unem year || line st_unem year, lpattern(dash) || line dy_unem year, lpattern(dot)
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_static_v_dynamic.png", replace
![One-Step-Ahead vs Dynamic Forecasting](week_11_static_v_dynamic.png)

<h5>Estimate RMSE and MSE</h5>
Calculate RMSE for Model 1
	gen e = unem - st_unem
	gen e2 = e^2
	sum e2 if test==1
	scalar esum= `r(sum)'/`r(N)'
	scalar model1_RMSE= esum^.5
	display model1_RMSE
Calculate MAE for Model 1
	gen e_abs = abs(e) if test ==1
	sum e_abs if test==1
	scalar model1_MAE=`r(mean)'
	display model1_MAE


One-step ahead we can use predict or forecast for VAR Model (lagged employment and lagged inflation)
Get 1997 estimates
	quietly reg unem l.unem l.inf if year < 1997
Our predict command will produce the same results as forecast solve, static
	predict p2
	estimates store model2
Forecast for Model 2
	forecast create forecast2, replace
	forecast estimates model2
One-Step-Ahead Forecast of Model 2
	forecast solve, prefix(st2_) begin(1997) end(2003) static
Dynamic Forecast of Model 2
	forecast solve, prefix(dy2_) begin(1997) end(2003) 
Calculate RMSE for Model 2
	drop e e2 e_abs
	gen e = unem - st2_unem
	gen e2 = e^2
	sum e2 if test==1
	scalar esum2 = `r(sum)'/`r(N)'
	scalar model2_RMSE=esum2^.5
	display model2_RMSE
Calculate MAE for Model 2
	gen e_abs = abs(e)
	sum e_abs if test==1
	scalar model2_MAE=`r(mean)'

Results
	display "Model 1: RMSE="model1_RMSE " and MAE="model1_MAE
	display "Model 2: RMSE="model2_RMSE " and MAE="model1_MAE
	display "Model 2 with lagged unemployment and lagged inflation has lower RMSE and MAE."

Forecasting Line
	twoway line unem year || line st_unem year, lpattern(dash) || line st2_unem year, lpattern(dot) ///
	title("Forecast of Unemployment") xtitle("Year") subtitle("One-Step-Ahead") ytitle("Unemployment Rate") xline(1996) ///
	legend(order(1 "Actual" 2 "Model 1" 3 "Model 2"))
	graph export "/Users/Sam/Desktop/Econ 645/Stata/week_11_forecast.png", replace
![Compare Forecasts](week_11_forecast.png)
